setenv bootlimit 3
setenv devtype mmc
setenv devnum 0

setenv bootcmd_resetvars 'setenv kernel_image; setenv ramdisk_image; setenv bootargs; setenv kernel_image2; setenv ramdisk_image2; setenv bootargs2'
setenv bootcmd_otenv 'run bootcmd_resetvars; ext4load ${devtype} ${devnum}:2 ${scriptaddr} /boot/loader/uEnv.txt; env import -t ${scriptaddr} ${filesize}'
setenv bootcmd_set_bootargs 'setenv bootargs "${bootargs} coherent_pool=1M 8250.nr_uarts=1 snd_bcm2835.enable_headphones=0 cgroup_disable=memory bcm2708_fb.fbwidth=656 bcm2708_fb.fbheight=416 bcm2708_fb.fbswap=1 vc_mem.mem_base=0x3ec00000 vc_mem.mem_size=0x40000000 panic=1 console=ttyS0,115200"'
setenv bootcmd_load_f 'ext4load ${devtype} ${devnum}:2 ${kernel_addr_r} ${kernel_image}; ext4load ${devtype} ${devnum}:2 ${ramdisk_addr_r} ${ramdisk_image}'
setenv bootcmd_run 'booti ${kernel_addr_r} ${ramdisk_addr_r} ${fdt_addr}'
setenv bootcmd_rollbackenv 'setenv kernel_image ${kernel_image2}; setenv ramdisk_image ${ramdisk_image2}; setenv bootargs ${bootargs2}'
setenv bootcmd_set_rollback 'if test ! "${rollback}" = "1"; then setenv rollback 1; setenv upgrade_available 0; saveenv; fi'
setenv bootostree 'run bootcmd_load_f; run bootcmd_run'
setenv altbootcmd 'run bootcmd_otenv; run bootcmd_set_rollback; if test -n "${kernel_image2}"; then run bootcmd_rollbackenv; run bootcmd_set_bootargs; fi; run bootostree; reset'

if test ! -e ${devtype} ${devnum}:1 uboot.env; then saveenv; fi

if test "${rollback}" = "1"; then run altbootcmd; else run bootcmd_otenv; run bootcmd_set_bootargs; run bootostree; if test ! "${upgrade_available}" = "1"; then setenv upgrade_available 1; saveenv; fi; reset; fi

reset